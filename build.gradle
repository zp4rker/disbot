plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.4.21'
    id 'org.jetbrains.kotlin.plugin.serialization' version '1.4.21'

    id 'maven'
    id 'signing'

    id 'io.codearte.nexus-staging' version '0.22.0'
}

group 'com.zp4rker'
archivesBaseName = project.name
version '1.2.2-release'

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    compile "org.jetbrains.kotlin:kotlin-reflect:1.4.21"

    compile "net.dv8tion:JDA:4.2.0_227"

    compile "ch.qos.logback:logback-classic:1.2.3"

    compile "org.fusesource.jansi:jansi:2.1.1"

    compile "org.jetbrains.kotlinx:kotlinx-serialization-core:1.0.1"
    compile "com.charleskorn.kaml:kaml:0.26.0"

    compile "com.vdurmont:emoji-java:5.1.1"
    compile "io.leego:banana:2.0.0"
}

compileJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

compileKotlin {
    kotlinOptions.jvmTarget = 1.8
}

task javadocJar(type: Jar) {
    archiveClassifier.set("javadoc")
    from javadoc
}

task sourcesJar(type: Jar) {
    archiveClassifier.set("sources")
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment d -> signing.signPom(d) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name "Discore"
                artifactId "discore"
                packaging "jar"
                description "A Discord bot core built upon JDA, written in Kotlin."
                url "https://github.com/zp4rker/discore"

                scm {
                    connection "scm:git:git://github.com/zp4rker/discore.git"
                    url "https://github.com/zp4rker/discore"
                }

                licenses {
                    license {
                        name "The Apache License, Version 2.0"
                        url "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }

                developers {
                    developer {
                        id "zp4rker"
                        name "Zaeem Parker"
                        email "iam@zp4rker.com"
                    }
                }
            }
        }
    }
}